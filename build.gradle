plugins {
	id "fabric-loom" version "0.5-SNAPSHOT"
	id "eclipse"
	id "maven-publish"
	id "com.github.breadmoirai.github-release" version "2.2.12"
	id "org.ajoberstar.grgit" version "4.1.0"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.modrinth.minotaur" version "1.1.0"
}

repositories {
	mavenLocal()
	maven {
		name = "Fabric"
		url = "https://maven.fabricmc.net/"
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		name = "CurseForge"
		url = "https://minecraft.curseforge.com/api/maven"
	}
	maven {
		name = "JitPack"
		url = "https://jitpack.io"
		content {
			includeGroup "com.github.Virtuoel"
		}
	}
	maven {
		name = "GitHub"
		url = "https://maven.pkg.github.com"
	}
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def ENV = System.getenv()

group = project.maven_group
archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + (ENV.BUILD_NUMBER ? "build.${ENV.BUILD_NUMBER}-" : "") + project.minecraft_version

minecraft {
	refmapName = "${archives_base_name}-refmap.json"
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
	
	def excludeLoader = { exclude group: "net.fabricmc", module: "fabric-loader" }
	def excludeApi = { exclude group: "net.fabricmc.fabric-api" }
	def excludeFabric = {
		exclude group: "net.fabricmc", module: "fabric-loader"
		exclude group: "net.fabricmc.fabric-api"
	}
	
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"
	if(project.hasProperty("fabric_version")) {
		if(!project.hasProperty("fabric_modules") && !project.hasProperty("api_fabric_modules") && !project.hasProperty("impl_fabric_modules")) {
			modCompileOnly "net.fabricmc.fabric-api:fabric-api:${fabric_version}", excludeLoader
			modRuntime "net.fabricmc.fabric-api:fabric-api:${fabric_version}", excludeLoader
		}
		
		if(project.hasProperty("fabric_modules")) {
			project.fabric_modules.split(", ").each { String module ->
				modCompileOnly fabricApi.module(module, project.fabric_version), excludeLoader
				modRuntime fabricApi.module(module, project.fabric_version), excludeLoader
			}
		}
		
		if(project.hasProperty("api_fabric_modules")) {
			project.api_fabric_modules.split(", ").each { String module ->
				modApi fabricApi.module(module, project.fabric_version), excludeLoader
			}
		}
		
		if(project.hasProperty("impl_fabric_modules")) {
			project.impl_fabric_modules.split(", ").each { String module ->
				modImplementation fabricApi.module(module, project.fabric_version), excludeLoader
			}
		}
		
		if(project.hasProperty("included_fabric_modules")) {
			project.included_fabric_modules.split(", ").each { String module ->
				include fabricApi.module(module, project.fabric_version)
			}
		}
	}
	
	if(project.hasProperty("modmenu_version")) {
		modCompileOnly "com.terraformersmc:modmenu:${modmenu_version}", excludeFabric
		modRuntime "com.terraformersmc:modmenu:${modmenu_version}", excludeFabric
	}
}

processResources {
	inputs.property "version", project.version
	
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

eclipse {
	project {
		name = archives_base_name
		buildCommand "org.eclipse.buildship.core.gradleprojectbuilder"
		natures "org.eclipse.buildship.core.gradleprojectnature"
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

jar {
	from "LICENSE"
	manifest
	{
		attributes "Implementation-Title": project.archivesBaseName
		attributes "Implementation-Version": project.version
		attributes "Maven-Artifact": "${project.group}:${project.archivesBaseName}:${project.version}"
		attributes "Built-On-Minecraft": "${project.minecraft_version}"
		attributes "Built-On-Java": "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})"
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
	manifest
	{
		attributes "Implementation-Title": project.archivesBaseName + "-sources"
		attributes "Implementation-Version": project.version
		attributes "Maven-Artifact": "${project.group}:${project.archivesBaseName}:${project.version}:sources"
		attributes "Built-On-Minecraft": "${project.minecraft_version}"
		attributes "Built-On-Java": "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})"
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				classifier = "sources"
				builtBy remapSourcesJar
			}
		}
	}
	
	repositories {
		mavenLocal()
		if(project.hasProperty("github_maven_owner")) {
			maven {
				url = "https://maven.pkg.github.com/${github_maven_owner}"
				
				credentials {
					username project.properties.github_username
					password project.properties.github_packages_token
				}
			}
		}
	}
}

githubRelease {
	token project.hasProperty("github_releases_token") ? project.github_releases_token : ""
	if(project.hasProperty("github_repository_owner")) {
		owner project.github_repository_owner
	}
	if(project.hasProperty("github_repository")) {
		repo project.github_repository
	}
	
	def versionName = project.version.replaceAll(/\+/, "_")
	
	tagName versionName
	targetCommitish { org.ajoberstar.grgit.Grgit.open(dir: ".").branch.current().name }
	releaseName versionName
	
	def filenameFilter = { dir, filename -> filename.contains(project.version) && !filename.contains("-dev.jar") }
	releaseAssets = jar.destinationDirectory.files().filter(filenameFilter).files
}

if(project.hasProperty("fabric_version")) {
	ext.changelog_fabric_version = project.fabric_version
}

String generateHTMLChangelog() {
	String changelog_text = ""
	
	File file = file("changelog.txt")
	if(file.exists())
	{
		file.eachLine { String line ->
			if(!line.isAllWhitespace())
			{
				changelog_text += "<p>${line.stripIndent()}</p>\n"
			}
		}
		changelog_text += "<p>&nbsp;</p>\n"
	}
	
	changelog_text += "<p>Last successfully tested on:</p>\n<p>Minecraft version <code>${minecraft_version}</code></p>\n<p>Yarn mappings <code>${yarn_mappings}</code></p>\n<p>Fabric Loader <code>${loader_version}</code></p>\n"
	
	if(project.hasProperty("changelog_fabric_version")) {
		changelog_text += "<p><a href=\"https://www.curseforge.com/minecraft/mc-mods/fabric-api/files/all\" rel=\"nofollow\">Fabric API <code>${changelog_fabric_version}</code></a></p>\n"
	}
	
	return changelog_text
}

curseforge {
	if(project.hasProperty("curseforge_api_key")) {
		apiKey = project.properties.curseforge_api_key
	}
	
	if(project.hasProperty("curseforge_id")) {
		project {
			id = project.curseforge_id
			
			releaseType = project.curseforge_release_type
			
			addGameVersion "Fabric"
			
			def display_name_prefix = ""
			if(project.hasProperty("curseforge_release_versions")) {
				def game_versions = project.curseforge_release_versions.split(", ")
				display_name_prefix = "[${game_versions[0]} - ${project.minecraft_version}] "
				game_versions.each { String gameVersion ->
					addGameVersion gameVersion
				}
			}
			
			mainArtifact(remapJar) {
				displayName = "${display_name_prefix}${archives_base_name} ${project.mod_version}"
				
				if(project.hasProperty("required_dep_slugs") || project.hasProperty("embedded_lib_slugs") || project.hasProperty("optional_dep_slugs") || project.hasProperty("compatible_tool_slugs") || project.hasProperty("incompatible_slugs")) {
					relations {
						if(project.hasProperty("required_dep_slugs")) {
							project.required_dep_slugs.split(", ").each { String slug ->
								requiredDependency slug
							}
						}
						
						if(project.hasProperty("embedded_lib_slugs")) {
							project.embedded_lib_slugs.split(", ").each { String slug ->
								embeddedLibrary slug
							}
						}
						
						if(project.hasProperty("optional_dep_slugs")) {
							project.optional_dep_slugs.split(", ").each { String slug ->
								optionalDependency slug
							}
						}
						
						if(project.hasProperty("compatible_tool_slugs")) {
							project.compatible_tool_slugs.split(", ").each { String slug ->
								tool slug
							}
						}
						
						if(project.hasProperty("incompatible_slugs")) {
							project.incompatible_slugs.split(", ").each { String slug ->
								incompatible slug
							}
						}
					}
				}
				
				changelogType = "html"
				changelog = project.generateHTMLChangelog()
			}
			
			addArtifact(sourcesJar) {
				displayName = "${archives_base_name} ${project.mod_version} Source Code"
			}
			
			afterEvaluate {
				uploadTask.dependsOn remapSourcesJar
			}
		}
		options {
			forgeGradleIntegration = false
		}
	}
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload) {
	if(project.hasProperty("modrinth_token")) {
		token = project.properties.modrinth_token
	}
	
	if(project.hasProperty("modrinth_id")) {
		projectId = project.modrinth_id
		
		releaseType = project.modrinth_release_type
		
		addLoader "fabric"
		
		if(project.hasProperty("modrinth_release_versions")) {
			project.modrinth_release_versions.split(", ").each { String gameVersion ->
				addGameVersion gameVersion
			}
		}
		
		versionName = "${archives_base_name} ${project.mod_version}"
		
		versionNumber = project.version
		
		uploadFile = remapJar
		addFile sourcesJar
	}
}
